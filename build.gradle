plugins {
  id "fabric-loom" version "1.10-SNAPSHOT"
  id "com.gradleup.shadow" version "9.0.0-beta8"
}

configurations {
  shade
  implementation.extendsFrom shade
}

repositories {
  maven {url("https://maven.wispforest.io/releases/")}
  maven {url("https://maven.terraformersmc.com/")}
}

dependencies {
  minecraft("com.mojang:minecraft:${project.minecraft_version}")
  mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")

  modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")
  modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")
  modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")

  compileOnly("org.projectlombok:lombok:1.18.36")
  annotationProcessor("org.projectlombok:lombok:1.18.36")

  annotationProcessor modImplementation("io.wispforest:owo-lib:${project.owo_version}")
  include("io.wispforest:owo-sentinel:${project.owo_version}")

  shade("com.hivemq:hivemq-mqtt-client-shaded:1.3.0")
}

processResources {
  filteringCharset "UTF-8"
  filesMatching("fabric.mod.json") {
    expand(
      "version": project.version,
      "minecraft_version": project.minecraft_version,
      "loader_version": project.loader_version,
      "fabric_version": project.fabric_version,
      "modmenu_version": project.modmenu_version,
      "owo_version": project.owo_version
    )
  }
}

shadowJar {
  archiveVersion = ""
  archiveClassifier = ""
  minimize()
  from sourceSets.main.output
  configurations = [project.configurations.shade]
  relocate("com.hivemq","japi.iotcraft.shadow.com.hivemq")
}
