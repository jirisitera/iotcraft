plugins {
  id "fabric-loom" version "1.10-SNAPSHOT"
  id "com.gradleup.shadow" version "9.0.0-beta8"
}

configurations {
  shade
  implementation.extendsFrom shade
}

repositories {
  maven {url("https://maven.wispforest.io/releases/")}
  maven {url("https://maven.terraformersmc.com/")}
  maven {url("https://maven.shedaniel.me/")}
}

dependencies {
  // loader
  minecraft("com.mojang:minecraft:${project.minecraft_version}")
  mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
  modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")
  // dependencies
  modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")
  modImplementation modApi("com.terraformersmc:modmenu:${project.modmenu_version}")
  modImplementation modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}")
  // libraries
  compileOnly("org.projectlombok:lombok:1.18.36")
  annotationProcessor("org.projectlombok:lombok:1.18.36")
  // shadow
  shade("com.hivemq:hivemq-mqtt-client-shaded:1.3.0")
}

processResources {
  filesMatching("fabric.mod.json") {
    expand(
      "version": project.version,
      "minecraft_version": project.minecraft_version,
      "loader_version": project.loader_version,
      "fabric_version": project.fabric_version,
      "modmenu_version": project.modmenu_version,
      "cloth_config_version": project.cloth_config_version
    )
  }
}

shadowJar {
  archiveVersion = ""
  archiveClassifier = ""
  minimize()
  from sourceSets.main.output
  configurations = [project.configurations.shade]
  relocate("com.hivemq","japi.iotcraft.shadow.com.hivemq")
}
